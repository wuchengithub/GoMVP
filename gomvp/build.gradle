apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

version = "1.3.2"

def siteUrl = 'https://github.com/wuchengithub/GoMVP'      // 项目的主页
def gitUrl = 'https://github.com/wuchengithub/GoMVP.git'   // Git仓库的url
group = "com.wookii.gomvp"
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'MVP for Android.'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'wuchengithub'     //填写的一些基本信息
                        name 'chenwu'
                        email 'wuchenxy2015@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "wookii"
        name = "GoMVP"   //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

/**
 * 生成aarTask
 */
task("build" + project.name) {
    group 'build'
    dependsOn 'assembleRelease'
    def outputName = project.name + version +".aar"
    def releaseName = project.name + "-release.aar"
    def aarPath = rootProject.getRootDir().path + "/aar"
    doLast {
        task(deleteAarFilesRelease, type: Delete) {
            delete aarPath + outputName
        }.execute()

        task(copyAarRelease, type: Copy) {
            from('build/outputs/aar') {
                include releaseName
            }
            into aarPath
            rename releaseName, outputName
        }.execute()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation libs.support_v4
    implementation libs.support_v7
    implementation libs.okhttp
    implementation libs.okhttp_logging
    implementation libs.retrofit
    implementation libs.retrofit_gson
    implementation libs.retrofit_rxjava
    implementation libs.rxandroid
    implementation libs.rxjava2
    implementation libs.gson
    implementation libs.support_annotation
    implementation libs.butternife
    kapt libs.butternife_processor
    implementation libs.aspectJ


}
